start S

// S = BachPreludeInC
S = { [>>2][BachPreludeInC] | bach-funky-backing }

bach-funky-backing = [x2][[x35][funk-var1]]

// https://michaelkravchuk.com/wp-content/uploads/2019/12/Bach-Prelude-In-C-Major-1.pdf
BachPreludeInC = ::i=piano 1 2 3 4 5 6 7 8 9 10 11 12 13 14 15 16 17 18 19 20 21 22 23 24 25 26 27 28 29 30 31 32 33 34 35

C6 = :e :g :5c
C6/4 = :4g :5c :5e
C4/2  = :bb :c :e
C74/3 = :3g :bb :e
Dm6/4 = :5a :5d :5f
D6 = :f# :5a :5d
D7no5 = :d :f# :5c
Edim7no5 = :e :g :5c#
F6 = :a :c :f
F#dim6 = :a :c :f#
G = :3g :b :d
G6/4 = :d :g :5b
G7no3 = :4g :5d :5f
G7no5 = :3g :b :f
Am = :a :c :e
Adim = :a :c :eb
Bdim6 = :d :f :5b
Copen = :c :g :5c
Dopen = [T2][Copen]
Eopen = [T4][Copen]
Fopen = [T5][Copen]
Gopen = [T7][Copen]
Aopen = [T9][Copen]
Bopen = [T11][Copen]


1 = [x2][{:c<8> | :_ :e<7> | :_<2> [x2][C6/4] }]
2 = [x2][:c :d [x2][Dm6/4]]
3 = [x2][:b :d [x2][G7no3]]
4 = 1
5 = [x2][:c :e [x2][Aopen]]
6 = [x2][:c :d [x2][D6]]
7 = [x2][:b :d [x2][Gopen]]
8 = [x2][:b :c [x2][C6]]
9 = [x2][:a :c [x2][C6]]
10 = [x2][:3d :a [x2][D7no5]]
11 = [x2][:3g :b [x2][G6/4]]
12 = [x2][:3g :bb [x2][Edim7no5]]
13 = [x2][:3f :a [x2][Dopen]]
14 = [x2][:3f :3ab [x2][Bdim6]]
15 = [x2][:3e :3g [x2][Copen]]
16 = [x2][:3e :3f [x2][F6]]
17 = [x2][:3d :3f [x2][F6]]
18 = [x2][:2g :3d [x2][G7no5]]
19 = [T-12][1]
20 = [x2][:3c :3g [x2][C4/2]]
21 = [x2][:2f :3f [x2][Am]]
22 = [x2][:2f# :3c [x2][Adim]]
23 = [x2][:2Ab :3f [x2][:b :c :d]]
24 = [x2][:2g :3f [x2][G]]
25 = [x2][:2g :3e [x2][[T-12][C6/4]]]
26 = [x2][:2g :3d [x2][:3g :c :f]]
27 = [x2][:2g :3d [x2][:3g :b :f]]
28 = [x2][:2g :3Eb [x2][F#dim6]]
29 = [x2][:2g :3e [x2][:3g :c :g]]
30 = 26
31 = 27
32 = [x2][:2c :3c [x2][C74/3]]

33 = :2c :3b :3f :a :c :f :c :a :c :a :3f :a :3f :3d :3f :3d
34 = :2c :3b :g :5b :5d :5f :5d :5b :5d :5b :g :5b :d :f :e :d
35 = [>>1/16][{ :2c | :3c | :e | :g | :5c }]



funk = funk-var1
funk = funk-var2
funk-4 = [x3][funk-var1] funk-var2
funk-var1 = { ::i=bassdrum bass-var1 | ::i=snare snare-var1 | ::i=hihatclosed hat-var1 }
funk-var2 = { ::i=bassdrum bass-var2 | ::i=snare snare-var2 | ::i=hihatclosed hat-var2 }

bass-var1 = bass1 bass2
bass-var2 = bass1 :_ :_
bass1 = b-note :_
bass2 = [>>2][:_ b-note] :_

hat-var1 = hat1 hat2
hat-var2 = hat1 :_ :_
hat1 = [x2][[>>2][[x2][::v=10 h-note]]]
hat2 = hat1

snare-var1 = snare1 snare2
snare-var2 = snare1 :_ [>>4][[x4][s-note]]
snare1 = :_ [>>4][s-note :_<2> s-note]
snare2 = [>>4][:_ s-note :_<2>] [>>2][s-note :_]

b-note = :3a
h-note = :e
s-note = :e
